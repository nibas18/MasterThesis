grammar dk.sdu.bdd.xtext.BddDsl with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate bddDsl "http://www.sdu.dk/bdd/xtext/BddDsl"

Model:
	('model' name = ID)
	(
		(modelRefs += ModelRef) |
		(declarativeEntityDef += DeclarativeEntityDef) |
		(imperativeEntityDef += ImperativeEntityDef) |
		(scenarios += Scenario)
	)*
;

ModelRef:
	'using' modelRef = [Model]
;


DeclarativeEntityDef:
	'declarative' 'entity' name = ID ('-'ID)? ('is' superEntities += [DeclarativeEntityDef] (',' superEntities += [DeclarativeEntityDef])*)? '{'
		('actions:' (actions += ActionDef) ((',') actions += ActionDef)*)?
		('states:' (states += StateDef) ((',') states += StateDef)*)?
		('properties:' (properties += PropertyDef) ((',') properties += PropertyDef)*)?
	'}'
;

ImperativeEntityDef:
	'imperative' 'entity' name = ID ('-'ID)? ('is' superEntities += [ImperativeEntityDef] (',' superEntities += [ImperativeEntityDef])*)? '{'
		('actions:' (actions += ImperativeActionDef)  ((',') actions += ImperativeActionDef)*)?
		('states:' (states += ImperativeStateDef) ((',') states += ImperativeStateDef)*)?
		('properties:' (properties += ImperativePropertyDef) ((',') properties += ImperativePropertyDef)*)?
	'}'
;

ImperativeActionDef:
	name = ID ((('and'|'the') ID)? | ID)  ('[' (preposition = PREP) ']')?	 ('the' argument = ID)?
;

ActionDef:
	name = ID (('and'|'the') ID)?  ('[' (preposition = PREP) ']')?	 ('the' argument = ID)?
;

PREP: ('on' | 'in' | 'from' | 'to' | 'into' | 'for' | 'with' | 'out' | 'off' | 'on' 'the' 'row');


StateDef: postive = StateName ('/' negative = StateName)?;
StateName: name = ID (('and'|'off') ID)?;

//Remove plural
PropertyDef: name = ID (ID?);

ImperativeStateDef: postive = ImperativeStateName PREP? ('/' negative = ImperativeStateName)?;
ImperativeStateName: name = (ID) ('and'|'off')?  (ID)?;

//Remove Plural
ImperativePropertyDef: name = ID (ID?);

//Multiple ID's for one name, Scenario-ref is not used, maybe string instead?
//Does not?
Scenario:
       'Scenario:' STRING
       'Given' preStateE = DeclarativeScenarioState
       imperative1 = ImperativeScenario?
       
       'When' actionE = DeclarativeScenarioAction 
	   imperative2 = ImperativeScenario?
       
       'Then' preStateE1 = DeclarativeScenarioState
	   imperative3 = ImperativeScenario?
;

//Match the scenario to the declarative 
ImperativeScenario:
   'which' 'means'
       
   'Given' preState = ImperativeScenarioState
   'When' action = ImperativeScenarioAction
   'Then' postState = ImperativeScenarioState
;

PrePostWords:
	('the'|'all' 'the'|'I')? ('do' 'not' | 'does' 'not')?
;

WhenWords:
	('I'|'the')? ('do' 'not'| 'does' 'not')?
;

DeclarativeScenarioState: 
	PrePostWords states += (DeclarativeEntityPropertyStatePhrase | DeclarativeEntityStatePhrase)
	('And' PrePostWords states += (DeclarativeEntityPropertyStatePhrase | DeclarativeEntityStatePhrase))*
;

ImperativeScenarioState:
	PrePostWords states += (ImperativeEntityPropertyStatePhrase | ImperativeEntityStatePhrase)
	('And' PrePostWords states += (ImperativeEntityPropertyStatePhrase | ImperativeEntityStatePhrase))*
;

DeclarativeScenarioAction:
	WhenWords actions += (DeclarativeEntityAction | VerbAction)
	('And' WhenWords actions += (DeclarativeEntityAction | VerbAction))*
;

ImperativeScenarioAction:
	WhenWords actions += ImperativeEntityAction
	('And' WhenWords actions += ImperativeEntityAction)*
;
 
//Maybe gather?
DeclarativeStatePhrase:
	epsp = DeclarativeEntityPropertyStatePhrase | esp = DeclarativeEntityStatePhrase
;

//Maybe gather?
ImperativeStatePhrase:
	epsp = ImperativeEntityPropertyStatePhrase | esp = ImperativeEntityStatePhrase
;

ENTITY_IDENTITY: ('#'INT) | STRING;


DeclarativeEntityRef:
	 entity = [DeclarativeEntityDef] name = STRING
;

ImperativeEntityRef:
	 entity = [ImperativeEntityDef] name = STRING
;

ActionRef:
	action = [ActionDef]
;

ImperativeActionRef:
	action = [ImperativeActionDef]
;

DeclarativeEntityStatePhrase:
	(property = PropertyRef? ('of'|'of' 'the')? ENTITY_IDENTITY? ('for'|'for' 'the')? ('of'|'of' 'the'|'for' 'the'|'for'))? 
	entity = DeclarativeEntityRef ('is'|'are') ('not')? state = [StateName] ID?	
;

ImperativeEntityStatePhrase:
	entity = ImperativeEntityRef 'is' state = [ImperativeStateName]
;



DeclarativeEntityPropertyStatePhrase:
	property = PropertyRef? ('of'|'of' 'the')? ENTITY_IDENTITY? ('of'|'for' |'from'|'for' 'the'|'of' 'the')  entity = DeclarativeEntityRef
	((('is'|'is' 'not') value = ENTITY_IDENTITY ID?) ID? | (('are'|'are' 'not') (values += ENTITY_IDENTITY) ID? | (',' values += ENTITY_IDENTITY)*) ID?) 
;

ImperativeEntityPropertyStatePhrase:
	 property = ImperativePropertyRef ('of'|'for'|'of' 'the'|'for' 'the') entity = ImperativeEntityRef 'is' propertyValue = STRING 
;

//Maybe gather?
DeclarativeActionPhrase:
	DeclarativeEntityAction | VerbAction
	// Deleted: | VerifyAction
;

VerbAction:
	action = DeclarativeActionRef 'and'? ID? 'on'? (ID'-'ID)? ('for' 'the'| 'the'|'on')? (entity = DeclarativeEntityRef)? ('on'|'off'|'in') entity2 = DeclarativeEntityRef (('on'|'off'|'in') | ('of'|'for'|'for' 'the'|'of' 'the')) PREP entity3 = DeclarativeEntityRef?
;

DeclarativeEntityAction: 
	entity = DeclarativeEntityRef actionRef = ActionRef PREP? ('the')? entity2 = DeclarativeEntityRef
;

ImperativeEntityAction: 
	entity = ImperativeEntityRef actionRef = ImperativeActionRef PREP? (propertyRef = ImperativePropertyRef)?
;

PropertyRef:
    property = [PropertyDef] (propertyValue = STRING)? (PREP? propertyValue2 = STRING property2 = [PropertyDef])? 
;
ImperativePropertyRef:
    property = [ImperativePropertyDef] (propertyValue = STRING)? (PREP propertyValue2 = STRING property2 = [ImperativePropertyDef])? 
;

DeclarativeActionRef: action = ActionRef 'on'? (argument = ENTITY_IDENTITY)? (preposition = PREP)? (('the'|'on')(entity = DeclarativeEntityRef))? ;

DOUBLE returns ecore::EDouble : INT'.'INT;
