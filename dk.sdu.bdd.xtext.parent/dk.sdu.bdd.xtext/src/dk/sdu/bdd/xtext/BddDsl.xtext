grammar dk.sdu.bdd.xtext.BddDsl with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate bddDsl "http://www.sdu.dk/bdd/xtext/BddDsl"

Model:
	('model' name = ID)
	(
		(modelRefs += ModelRef) |
		(declarativeEntityDef += DeclarativeEntityDef) |
		(imperativeEntityDef += ImperativeEntityDef) |
		(scenarios += Scenario)
	)*
;

ModelRef:
	'using' modelRef = [Model]
;


DeclarativeEntityDef:
	'declarative' 'entity' name = ID ('-'ID)? ('is' superEntities += [DeclarativeEntityDef] (',' superEntities += [DeclarativeEntityDef])*)? '{'
		('actions:' (actions += ActionDef) ((',') actions += ActionDef)*)?
		('states:' (states += StateDef) ((',') states += StateDef)*)?
		('properties:' (properties += PropertyDef) ((',') properties += PropertyDef)*)?
	'}'
;

ImperativeEntityDef:
	'imperative' 'entity' name = ID ('-'ID)? ('is' superEntities += [ImperativeEntityDef] (',' superEntities += [ImperativeEntityDef])*)? '{'
		('actions:' (actions += ImperativeActionDef)  ((',') actions += ImperativeActionDef)*)?
		('states:' (states += ImperativeStateDef) ((',') states += ImperativeStateDef)*)?
		('properties:' (properties += ImperativePropertyDef) ((',') properties += ImperativePropertyDef)*)?
	'}'
;

ImperativeActionDef:
	name = ID ((('and'|'the') ID)? | ID)  ('[' (preposition = PREP) ']')?	 ('the' argument = ID)?
;

ActionDef:
	name = ID (('and'|'the') ID)?  ('[' (preposition = PREP) ']')?	 ('the' argument = ID)?
;

PREP: ('on' | 'of' | 'in' | 'from' | 'to' | 'into' | 'for' | 'with' | 'out' | 'off' | 'on' 'the' 'row');


StateDef: postive = StateName ('/' negative = StateName)?;
StateName: name = ID (('and'|'off') ID)?;

PropertyDef: name = ID (ID?);

ImperativeStateDef: postive = ImperativeStateName ('/' negative = ImperativeStateName)?;
ImperativeStateName: name = ID (('and'|'off') ID)?;

ImperativePropertyDef: name = ID (ID?);

Scenario:
       'Scenario:' STRING
       'Given' preStateE = DeclarativeScenarioState
       imperative1 = ImperativeScenario?
       
       'When' actionE = DeclarativeScenarioAction 
	   imperative2 = ImperativeScenario?
       
       'Then' preStateE1 = DeclarativeScenarioState
	   imperative3 = ImperativeScenario?
;

ImperativeScenario:
   'which' 'means'
       
   'Given' preState = ImperativeScenarioState
   'When' action = ImperativeScenarioAction
   'Then' postState = ImperativeScenarioState
;

PrePostWords:
	('the'|'all'|'I'|'I' 'do' 'not')? 
;

WhenWords:
	('I' | 'I' 'do' 'not' |'the')?
;

DeclarativeScenarioState: 
	PrePostWords states += (DeclarativeEntityPropertyStatePhrase | DeclarativeEntityStatePhrase)
	('And' PrePostWords states += (DeclarativeEntityPropertyStatePhrase | DeclarativeEntityStatePhrase))*
;

ImperativeScenarioState:
	PrePostWords states += (ImperativeEntityPropertyStatePhrase | ImperativeEntityStatePhrase)
	('And' PrePostWords states += (ImperativeEntityPropertyStatePhrase | ImperativeEntityStatePhrase))*
;

DeclarativeScenarioAction:
	WhenWords actions += (DeclarativeEntityAction | VerbAction)
	('And' WhenWords actions += (DeclarativeEntityAction | VerbAction))*
;

ImperativeScenarioAction:
	WhenWords actions += (ImperativeEntityAction | ImperativeVerbAction)
	('And' WhenWords actions += (ImperativeEntityAction | ImperativeVerbAction))*
;

ENTITY_IDENTITY: (('#' INT) | STRING);
ADVERB: ('linearly');

DeclarativeEntityRef:
	 entity = [DeclarativeEntityDef] (entityValue = ENTITY_IDENTITY)?
;

ImperativeEntityRef:
	 entity = [ImperativeEntityDef] (entityValue = ENTITY_IDENTITY)?
;

ActionRef:
	action = [ActionDef]
;

ImperativeActionRef:
	action = [ImperativeActionDef]
;

PropertyRef:
    property = [PropertyDef] (propertyValue = ENTITY_IDENTITY)?
;

ImperativePropertyRef:
    property = [ImperativePropertyDef] (propertyValue = ENTITY_IDENTITY)?
;

DeclarativeEntityStatePhrase:
	entity = DeclarativeEntityRef ('is'|'are') ('not')? state = [StateName]	
;

ImperativeEntityStatePhrase:
	entity = ImperativeEntityRef ('is'|'are') ('not')? state = [ImperativeStateName]
;

DeclarativeEntityPropertyStatePhrase:
	property = PropertyRef? (PREP|PREP 'the')  entity = DeclarativeEntityRef
	(('is'|'is' 'not')  | ('are'|'are' 'not')) value = ENTITY_IDENTITY
;

ImperativeEntityPropertyStatePhrase:
	 property = ImperativePropertyRef? (PREP|PREP 'the')  entity = ImperativeEntityRef
	(('is'|'is' 'not')  | ('are'|'are' 'not')) value = ENTITY_IDENTITY
;

VerbAction:
	action = ActionRef (PREP 'the' | 'the' | PREP)? (entity = DeclarativeEntityRef)? ((PREP 'the' | 'the' | PREP)? entity2 = DeclarativeEntityRef)? ((PREP 'the' | 'the' | PREP)? entity3 = DeclarativeEntityRef)?
;

DeclarativeEntityAction: 
	entity = DeclarativeEntityRef actionRef = ActionRef PREP? ('the')? entity2 = DeclarativeEntityRef
;

ImperativeVerbAction:
	action = ImperativeActionRef (PREP 'the' | 'the' | PREP)? (entity = ImperativeEntityRef)? ((PREP 'the' | 'the' | PREP)? entity2 = ImperativeEntityRef)? ((PREP 'the' | 'the' | PREP)? entity3 = ImperativeEntityRef)? 
;

ImperativeEntityAction: 
	entity = ImperativeEntityRef actionRef = ImperativeActionRef PREP? ('the')? entity2 = ImperativeEntityRef
;

DOUBLE returns ecore::EDouble : INT'.'INT;
